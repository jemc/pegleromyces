
import 'fixtures/MycoGrammar.my'

BasicSpec {
  name: "BytecodeMachine"
  
  new_machine: BytecodeMachine { }
  
  [tests]
  
  it "can match from a tiny grammar": {
    machine = new_machine
    machine.grammar = Grammar {
      [rules]
      rule root:
        str('abc') / str('x') + set('Yy') + !str('y') + !!range('a','z') + any
    }
    machine.subject = 'xyz'
    machine.execute
    assert_equal(machine.result, 3)
  }
  
  it "can match from a grammar with multiplicit patterns": {
    machine = new_machine
    machine.grammar = Grammar {
      [rules]
      rule root: str('x').+ + str('y').* + str('z').-
    }
    machine.subject = 'xxz'
    machine.execute
    assert_equal(machine.result, 3)
  }
  
  it "can match from a grammar with rule calls": {
    machine = new_machine
    machine.grammar = Grammar {
      [rules]
      rule root: unintended / intended
      rule unintended: abc
      rule intended: x + any_y + not_y + any_lower
      
      rule abc:   str('abc')
      rule x:     str('x')
      rule any_y: set('Yy')
      rule not_y: !str('y')
      rule lower: range('a','z')
      rule any_lower: !!lower + any
    }
    machine.subject = 'xyz'
    machine.execute
    assert_equal(machine.result, 3)
  }
  
  it "can match from the Myco grammar": {
    machine = new_machine
    machine.grammar = MycoGrammar
    machine.subject = '
    Object {
      foo:    one
      bar  :  Two  
      baz    :3
      deci  : 3.88
      ary :  [1,2, 3,
              4
              5,,
              6
              
              7,
             ]
      all:null
      none:void
      str  : "string"
      sym  :  :bol
      ssym : :"with spaces"
      s  :  self
      t:    true
      f:    false
      "x y" : z
    }'
    machine.execute
    assert(machine.result)
  }
}
