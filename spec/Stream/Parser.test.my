
BasicSpec {
  name: "Stream::Parser"
  
  new_parser: Stream::Parser.new
  
  [tests]
  
  it "can match from a minimal grammar": {
    parser = new_parser
    parser.grammar = Grammar {
      [rules]
      rule root: si.+
    }
    state = parser.parse([:x, :y, :z])
    assert_equal(state.end_idx, 3)
  }
  
  it "can match from a minimal grammar with rules in the stream items": {
    parser = new_parser
    parser.grammar = Grammar {
      [rules]
      rule root: si([:to_sym, [], :x]).+
    }
    state = parser.parse([:x, :x, :x])
    assert_equal(state.end_idx, 3)
  }
  
  it "can create a result from reductions": {
    parser = new_parser
    parser.grammar = Grammar {
      [rules]
      rule root: r(si.+[:list]) { list.join('.') }[:root]
    }
    state = parser.parse([:x, :y, :z])
    assert_equal(state.result[:root], 'x.y.z')
  }
  
  it "captures individual items for single-item captures": {
    parser = new_parser
    parser.grammar = Grammar {
      [rules]
      rule root: r(si[:a] + si[:b] + si.+[:rest]) { [a, b, rest] }[:root]
    }
    state = parser.parse([:x, :y, :z])
    assert_equal(state.result[:root], [:x, :y, [:z]])
  }
  
  it "can find the first occurrence": {
    parser = new_parser
    parser.grammar = Grammar {
      [rules]
      rule root: si([:to_sym, [], :y]).+
    }
    state = parser.find([:x, :x, :y, :y, :z, :z, :y, :y, :x, :x])
    assert_equal(state.start_idx, 2)
    assert_equal(state.end_idx, 4)
  }
  
  it "can find the next occurrence": {
    parser = new_parser
    parser.grammar = Grammar {
      [rules]
      rule root: si([:to_sym, [], :y]).+
    }
    state = parser.find([:x, :x, :y, :y, :z, :z, :y, :y, :x, :x], start_idx: 4)
    assert_equal(state.start_idx, 6)
    assert_equal(state.end_idx, 8)
  }
  
  it "can scan the occurrences": {
    parser = new_parser
    parser.grammar = Grammar {
      [rules]
      rule root: si([:to_sym, [], :y]).+
    }
    states = parser.scan([:x, :x, :y, :y, :z, :z, :y, :y, :x, :x])
    assert_equal(states.size, 2)
    assert_equal(states[0].start_idx, 2)
    assert_equal(states[0].end_idx, 4)
    assert_equal(states[1].start_idx, 6)
    assert_equal(states[1].end_idx, 8)
  }
  
  it "can scan the next occurrences": {
    parser = new_parser
    parser.grammar = Grammar {
      [rules]
      rule root: si([:to_sym, [], :y]).+
    }
    states = parser.scan([:x, :x, :y, :y, :z, :z, :y, :y, :x, :x], start_idx: 3)
    assert_equal(states.size, 2)
    assert_equal(states[0].start_idx, 3)
    assert_equal(states[0].end_idx, 4)
    assert_equal(states[1].start_idx, 6)
    assert_equal(states[1].end_idx, 8)
  }
  
  it "can scan and replace the occurrences": {
    parser = new_parser
    parser.grammar = Grammar {
      [rules]
      rule ys:   si([:to_sym, [], :y]).+
      rule ys2Y: r(ys) { :Y }
      rule root: ys2Y[:root]
    }
    assert_equal(
      parser.scan_replace([:x, :x, :y, :y, :z, :z, :y, :y, :x, :x])
                          [:x, :x,   :Y,   :z, :z,   :Y,   :x, :x]
    )
  }
}
